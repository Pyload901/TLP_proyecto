Programa -> Bloque Programa | Declaracion_funcion Programa | e
Declaracion_funcion -> proc Tipo_dato Id ( Parametros_funcion ) Bloque
Parametros_funcion -> Tipo_dato Id Parametros_funcion_aux | e
Parametros_funcion_aux -> , Tipo_dato Id Parametros_funcion_aux | e
Retorno_funcion -> return Expresion
Bloque -> start BloqueAux end
BloqueAux -> Instruccion BloqueAux | e
Instruccion -> Asignacion ; | Declaracion ; | For | While | If | Exec_funcion ; | Retorno_funcion ;
Asignacion -> Id = Expresion | Id = [ Lista_valores ]
Lista_valores -> Expresion Lista_valores_aux | e
Lista_valores_aux -> , Expresion Lista_valores_aux | e
Declaracion -> Tipo_dato Id | Tipo_dato Id = Expresion | Tipo_dato Id [ Numero_int ] | Tipo_dato Id [ Numero_int ] = [ Lista_valores ]
For -> for ( Declaracion ; Expresion; Asignacion) Bloque
While -> while ( Expresion ) Bloque

If -> if ( Expresion ) BloqueElseOpt
BloqueElseOpt -> Bloque ElseOpt
ElseOpt -> else Bloque | e

Exec_funcion -> exec Id ( Argumentos )
Argumentos -> Expresion ArgumentosAux | e
ArgumentosAux -> , Expresion ArgumentosAux | e

Expresion -> OrExpresion
OrExpresion -> AndExpresion OrExpresionAux
OrExpresionAux -> || AndExpresion OrExpresionAux | e
AndExpresion -> NotExpresion AndExpresionAux
AndExpresionAux -> && NotExpresion AndExpresionAux | e
NotExpresion -> ! NotExpresion | Expresion_relacional
Expresion_relacional -> Expresion_suma Expresion_relacionalAux
Expresion_relacionalAux -> Op_relacional Expresion_suma | e

Expresion_suma -> Expresion_multiplicacion Expresion_sumaAux
Expresion_sumaAux -> + Expresion_multiplicacion Expresion_sumaAux | - Expresion_multiplicacion Expresion_sumaAux | e
Expresion_multiplicacion -> Termino TerminoAux
TerminoAux -> * Termino TerminoAux | / Termino TerminoAux | % Termino TerminoAux | e
Termino -> - Factor | Factor

Factor -> Id ArrayIndexOpt | Valor | ( Expresion )
ArrayIndexOpt -> [ Expresion ] | e
Op_relacional -> == | != | < | > | <= | >=
Valor -> Numero_int | Numero_float | Caracter | Booleano
Tipo_dato -> int | bool | char | float
Booleano -> true | false
Numero_int -> VALOR
Numero_float -> DECIMAL
Caracter -> LETRA
Id -> ID