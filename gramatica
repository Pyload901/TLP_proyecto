Programa -> Bloque Programa | Declaracion_funcion Programa | λ
Declaracion_funcion -> TIPO FUNCTION ID LPAREN Parametros_funcion RPAREN Bloque
Parametros_funcion -> TIPO ID Parametros_funcion_aux | λ
Parametros_funcion_aux -> COMMA TIPO ID Parametros_funcion_aux | λ
Retorno_funcion -> RETURN Expresion
Bloque -> START BloqueAux END
BloqueAux -> Instruccion BloqueAux | λ
Instruccion -> Asignacion SEMICOLON | Declaracion SEMICOLON | For | While | If | Exec_funcion SEMICOLON | Retorno_funcion SEMICOLON

Asignacion -> ID ASSIGN AsignacionAux
AsignacionAux -> Expresion
                | LBRACKET Lista_valores RBRACKET
Declaracion -> TIPO ID DeclaracionAux
DeclaracionAux -> ASSIGN DeclaracionAssignAux
                    | LBRACKET Numero_int RBRACKET
                    | λ
DeclaracionAssignAux -> Expresion
                    | LBRACKET Lista_valores RBRACKET

Lista_valores -> Expresion Lista_valores_aux | λ
Lista_valores_aux -> COMMA Expresion Lista_valores_aux | λ
For -> FOR LPAREN Declaracion SEMICOLON Expresion SEMICOLON Asignacion RPAREN Bloque
While -> WHILE LPAREN Expresion RPAREN Bloque

If -> IF LPAREN Expresion RPAREN BloqueElseOpt
BloqueElseOpt -> Bloque ElseOpt
ElseOpt -> ELSE Bloque | λ

Exec_funcion -> EXEC ID LPAREN Argumentos RPAREN
Argumentos -> Expresion ArgumentosAux | λ
ArgumentosAux -> COMMA Expresion ArgumentosAux | λ

Expresion -> OrExpresion
OrExpresion -> AndExpresion OrExpresionAux | AndExpresion OrExpresionAux
OrExpresionAux -> OR AndExpresion OrExpresionAux | λ
AndExpresion -> NotExpresion AndExpresionAux
AndExpresionAux -> AND NotExpresion AndExpresionAux | λ
NotExpresion -> NOT NotExpresion | Expresion_relacional
Expresion_relacional -> Expresion_suma Expresion_relacionalAux
Expresion_relacionalAux -> Op_relacional Expresion_suma | λ

Expresion_suma -> Expresion_multiplicacion Expresion_sumaAux
Expresion_sumaAux -> ADD Expresion_multiplicacion Expresion_sumaAux | MINUS Expresion_multiplicacion Expresion_sumaAux | λ
Expresion_multiplicacion -> Termino TerminoAux
TerminoAux -> MULT Termino TerminoAux | DIV Termino TerminoAux | MODULO Termino TerminoAux | λ
Termino -> MINUS Factor | Factor

Factor -> ID ArrayIndexOpt | Valor | LPAREN Expresion RPAREN
ArrayIndexOpt -> LBRACKET Expresion RBRACKET | λ
Op_relacional -> EQ | NEQ | LT | GT | LEQ | GEQ
Valor -> INTVAL | DOUBLEVAL | CARACTER | Booleano
Booleano -> TRUE | FALSE